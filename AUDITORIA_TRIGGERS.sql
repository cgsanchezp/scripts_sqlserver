-- EJEMPLO DE TRIGGERS 

-------------- CONCEPTO GENERAL ----


USE master
GO
IF EXISTS (SELECT NAME FROM sys.databases 
           WHERE name = 'auditoria')
  begin
    DROP DATABASE auditoria
  end


CREATE TABLE DBO.PRUEBA (ID INT)
GO

CREATE TABLE DBO.PRUEBA2 (ID INT)
GO


CREATE TRIGGER DBO.TRE1 ON PRUEBA
FOR INSERT
AS

 DECLARE @ID INT
 SELECT @ID = ID FROM INSERTED
 
 INSERT INTO DBO.PRUEBA2 VALUES(@ID)
 
GO 

-- TEST

SELECT * FROM dbo.PRUEBA2

INSERT INTO PRUEBA 
VALUES (1)

SELECT * FROM dbo.PRUEBA2
SELECT * FROM dbo.PRUEBA


INSERT INTO PRUEBA 
VALUES (2),(3),(4)

SELECT * FROM dbo.PRUEBA2
SELECT * FROM dbo.PRUEBA





---------------------------------------------------------

--------- USANDO LOS TRIGGERS EN CONJUNTOS DE RESULTADOS

           

CREATE DATABASE auditoria
GO

USE auditoria
GO

CREATE TABLE DBO.AUDIT_CUSTOMER (ROWNUMBER INT IDENTITY,
                        FECHA DATETIMEOFFSET
                        DEFAULT(GETDATE()),
                        USUARIO VARCHAR(300)
                        DEFAULT(SUSER_SNAME()),
                        APLICACION VARCHAR(300)
                        DEFAULT(APP_NAME()),
                        HOST VARCHAR(300)
                        DEFAULT (HOST_NAME()),
                        TIPO CHAR(1)
                        CHECK (TIPO IN('D','U','I')),
                        CODIGO INT,NOMBRE VARCHAR(100),
                        NOMBRE_OLD VARCHAR(100))
GO

CREATE TABLE DBO.CUSTOMERS
(CODIGO INT PRIMARY KEY, NOMBRE VARCHAR(100))
GO


-- CREAMOS TRIGGER DE AUDITORIA

CREATE TRIGGER DBO.TRX1 ON DBO.CUSTOMERS FOR INSERT
AS
SET NOCOUNT ON

	INSERT INTO DBO.AUDIT_CUSTOMER
	(CODIGO,NOMBRE,TIPO)
	SELECT CODIGO,NOMBRE,'I' FROM INSERTED

GO

CREATE TRIGGER DBO.TRX2 ON DBO.CUSTOMERS FOR UPDATE
AS
SET NOCOUNT ON

	INSERT INTO DBO.AUDIT_CUSTOMER
	(CODIGO,NOMBRE,NOMBRE_OLD,TIPO)
	SELECT I.CODIGO,I.NOMBRE,D.NOMBRE,'U'
	FROM INSERTED I
	INNER JOIN DELETED D ON
	I.CODIGO = D.CODIGO
	AND I.NOMBRE <> D.NOMBRE
GO
                        

CREATE TRIGGER DBO.TRX3 ON DBO.CUSTOMERS FOR DELETE
AS
SET NOCOUNT ON

	INSERT INTO DBO.AUDIT_CUSTOMER
	(CODIGO,NOMBRE,TIPO)
	SELECT CODIGO,NOMBRE,'D' FROM DELETED

GO
                        
                        
--------------------  HACEMOS LAS PRUEBAS

-- VEMOS LA TABLA AUDIT


SELECT * FROM DBO.AUDIT_CUSTOMER C

-- INSERTAMOS 2 REGISTROS

INSERT INTO dbo.CUSTOMERS 
(CODIGO,NOMBRE)
VALUES (1,'SQLTOTAL'),(2,'MUG ARGENTINA')

SELECT * FROM DBO.AUDIT_CUSTOMER C

-- CAMBIAMOS EL NOMBRE DEL REGISTRO 1

UPDATE dbo.CUSTOMERS 
SET NOMBRE = 'SQLTOTAL CONSULTING'                         
WHERE CODIGO = 1

-- BORRAMOS EL REGISTRO 2

DELETE FROM dbo.CUSTOMERS
WHERE CODIGO =2

SELECT * FROM DBO.AUDIT_CUSTOMER C
ORDER BY FECHA 
