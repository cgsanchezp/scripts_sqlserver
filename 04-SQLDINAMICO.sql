--- SQL DINAMICO Y CONTEXTO DE SEGURIDAD
----------------------------------------



USE MASTER
GO

CREATE DATABASE TEST1 
GO

USE TEST1 
GO

-- CREAMOS UN NUEVO LOGIN

CREATE LOGIN FEDERICO WITH PASSWORD ='PASSW@RD'
GO

USE TEST1 
GO

-- CREAMOS EL USUARIO EN LA BASE DE DATOS

CREATE USER FEDERICO 
GO

-- CREAMOS UNA TABLA

CREATE TABLE DBO.EMPLEADOS (ID INT IDENTITY, NAME VARCHAR(50))
GO

-- CREAMOS UN STORE QUE CONSUME LA TABLA EMPLEADOS

CREATE PROC DBO.USP_EMPLEADOS_GET_ALL AS
 SELECT * FROM DBO.EMPLEADOS 
GO
-- CREAMOS OTRO STORE QUE CONSUME LA TABLA PERO CON SQLDINAMICO

CREATE PROC DBO.USP_EMPLEADOS_GET_ALL2 AS
 DECLARE @N NVARCHAR(50)
 SET @N = N'SELECT * FROM DBO.EMPLEADOS'
 EXECUTE SP_EXECUTESQL @N 
GO

-- FEDERICO SOLO TIENE PERMISOS A LOS STORES

GRANT EXECUTE ON DBO.USP_EMPLEADOS_GET_ALL TO FEDERICO

GRANT EXECUTE ON DBO.USP_EMPLEADOS_GET_ALL2 TO FEDERICO 
GO

-- ENTRAMOS COMO FEDERICO

EXECUTE AS LOGIN = 'FEDERICO'

SELECT SUSER_SNAME()

SELECT * FROM DBO.EMPLEADOS -- FALLA PORQUE NO TENEMOS PERMISO

EXEC DBO.USP_EMPLEADOS_GET_ALL -- FUNCIONA

EXEC DBO.USP_EMPLEADOS_GET_ALL2 -- FALLA POR EL SQL-DINAMICO

REVERT -- REVERTIMOS EL LOGIN

------------------------------------------------------------
--- SQL DINAMICO Y CONTEXTO DE SEGURIDAD
------------------------------------------------------------

-- CAMBIOS EL STORE DEL SQL DINAMO PARA QUE SE EJECUTE EN

-- OTRO CONTEXTO DE SEGURIDAD

ALTER PROC DBO.USP_EMPLEADOS_GET_ALL2 
WITH EXECUTE AS OWNER
AS

DECLARE @N NVARCHAR(50)
SET @N = N'SELECT * FROM DBO.EMPLEADOS'

EXECUTE SP_EXECUTESQL @N 
GO

-- VOLVEMOS A PROBAR

EXECUTE AS LOGIN = 'FEDERICO'

SELECT SUSER_SNAME()

SELECT * FROM DBO.EMPLEADOS -- FALLA PORQUE NO TENEMOS PERMISO

EXEC DBO.USP_EMPLEADOS_GET_ALL 

EXEC DBO.USP_EMPLEADOS_GET_ALL2 

REVERT


